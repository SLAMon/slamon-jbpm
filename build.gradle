buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'maven-publish'

def getVersion = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

group = 'org.slamon'
version = getVersion()

sourceCompatibility = 1.7
targetCompatibility = 1.7

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    outputs.upToDateWhen { false }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'http://repository.jboss.org/nexus/content/groups/public/'
    }
}

dependencies {
    compile 'org.kie:kie-api:6.2.0.Final'
    compile 'org.drools:drools-compiler:6.2.0.Final'
    compile 'com.google.http-client:google-http-client:1.19.0'
    compile 'com.google.http-client:google-http-client-gson:1.19.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
}

task getDepsCompile(type: Copy) {
    from configurations.compile
    into "$buildDir/libs"
}

task getDepsTest(type: Copy) {
    from configurations.testCompile
    into "$buildDir/libs"
}

task getDeps {
    dependsOn "getDepsCompile"
    dependsOn "getDepsTest"
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'java'
        name = 'slamon-jbpm'
        userOrg = System.getenv('BINTRAY_ORG')
        desc = 'SLAMon jBPM WorkItemHandler'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/SLAMon/slamon-jbpm'
        issueTrackerUrl = 'https://github.com/SLAMon/slamon-jbpm/issues'
        vcsUrl = 'https://github.com/SLAMon/slamon-jbpm.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
}